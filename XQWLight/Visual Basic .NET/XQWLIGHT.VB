' XQWLIGHT.VB - Source Code for XiangQi Wizard Light, Part V
'
' XiangQi Wizard Light - a Chinese Chess Program for Windows CE
' Designed by Morning Yellow, Version: 1.13, Last Modified: Dec. 2007
' Copyright (C) 2004-2007 www.elephantbase.net
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License along
' with this program; if not, write to the Free Software Foundation, Inc.,
' 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

Module XQWLight
    Public Const SND_ASYNC As Long = 1
    Public Const SND_NOWAIT As Long = 8192
    Public Const SND_FILENAME As Long = 131072
    Public Declare Function PlaySound Lib "COREDLL.DLL" (ByVal szSound As String, ByVal hMod As IntPtr, ByVal flags As Integer) As Integer
    Public Declare Function PlaySoundA Lib "WINMM.DLL" (ByVal szSound As String, ByVal hmod As Integer, ByVal flags As Integer) As Integer

    Public Const DRAW_SELECTED As Boolean = True
    Public Const RESULT_COMPUTER As Boolean = True

    Public Const LEFT_MARGIN As Integer = 6
    Public Const TOP_MARGIN As Integer = 8

    Public App_frm As MainForm, App_szPath As String
    Public App_bExit As Boolean, App_bFlipped As Boolean, App_bSound As Boolean
    Public App_nHandicap As Integer, App_nLevel As Integer
    Public App_sqCursor As Integer, App_sqSelected As Integer, App_mvLast As Integer

    Public Function AddMove(ByVal sqSrc As Integer, ByVal sqDst As Integer) As Boolean
        Dim mv As Integer, pcCaptured As Integer
        mv = sqSrc + (sqDst << 8)
        If Search_pos.LegalMove(mv) Then
            pcCaptured = Search_pos.pcSquares(sqDst)
            If Search_pos.MakeMove(mv) Then
                If pcCaptured > 0 Then
                    Search_pos.SetIrrev()
                End If
                App_sqSelected = 0
                App_mvLast = mv
                App_frm.DrawSquare(sqSrc, DRAW_SELECTED)
                App_frm.DrawSquare(sqDst, DRAW_SELECTED)
                If (Search_pos.mvsList(Search_pos.nMoveNum - 1).bCheck) Then
                    PlaySound("CHECK")
                Else
                    If (pcCaptured > 0) Then
                        PlaySound("CAPTURE")
                    Else
                        PlaySound("MOVE")
                    End If
                End If
                Return True
            Else
                PlaySound("ILLEGAL")
            End If
        End If
        Return False
    End Function

    Public Sub PlaySound(ByVal szWavFile As String)
        If Not App_bSound Then
            Return
        End If
        Try
            PlaySound(App_szPath + "SOUNDS\" + szWavFile + ".WAV", IntPtr.Zero, SND_ASYNC + SND_NOWAIT + SND_FILENAME)
        Catch ex As Exception
            PlaySoundA(App_szPath + "SOUNDS\" + szWavFile + ".WAV", 0, SND_ASYNC + SND_NOWAIT + SND_FILENAME)
        End Try
    End Sub

    Public Function GetResult(Optional ByVal szWavFile As String = "") As Boolean
        Dim vlRep As Integer
        If Search_pos.IsMate() Then
            MsgBox(IIf(szWavFile = "", "祝贺你取得胜利！", "请再接再厉！"), MsgBoxStyle.Information, "棋局结束")
            Return True
        End If
        vlRep = Search_pos.RepStatus(3)
        If vlRep > 0 Then
            vlRep = Search_pos.RepValue(vlRep)
            If szWavFile <> "" Then
                vlRep = -vlRep
            End If
            MsgBox(Switch(vlRep > WIN_VALUE, "长打作负，请不要气馁！", vlRep < -WIN_VALUE, "电脑长打作负，祝贺你取得胜利！", True, "双方不变作和，辛苦了！"), MsgBoxStyle.Information, "棋局结束")
            Return True
        End If
        If Search_pos.nMoveNum = 100 Then
            MsgBox("超过自然限着作和，辛苦了！", MsgBoxStyle.Information, "棋局结束")
            Return True
        End If
        If szWavFile <> "" Then
            PlaySound(szWavFile)
        End If
        Return False
    End Function

    Public Function ResponseMove() As Boolean
        Dim sqLast As Integer, pcCaptured As Integer, szWavFile As String
        If GetResult() Then
            Return False
        End If
        If ((App_mvLast >> 8) And 15) < 8 Xor App_bFlipped Then
            App_frm.imgThinking.Left = LEFT_MARGIN + 112
        Else
            App_frm.imgThinking.Left = LEFT_MARGIN
        End If
        If ((App_mvLast >> 8) >> 4) < 8 Xor App_bFlipped Then
            App_frm.imgThinking.Top = TOP_MARGIN + 128
        Else
            App_frm.imgThinking.Top = TOP_MARGIN
        End If
        App_frm.imgThinking.Visible = True
        App_frm.Refresh()
        SearchMain(CInt(4 ^ App_nLevel))
        App_frm.imgThinking.Visible = False
        If App_mvLast > 0 Then
            App_frm.DrawSquare(App_mvLast And 255)
            App_frm.DrawSquare(App_mvLast >> 8)
        End If
        szWavFile = "MOVE2"
        pcCaptured = Search_pos.pcSquares(Search_mvResult >> 8)
        Search_pos.MakeMove(Search_mvResult)
        If pcCaptured > 0 Then
            szWavFile = "CAPTURE2"
            Search_pos.SetIrrev()
        End If
        If Search_pos.mvsList(Search_pos.nMoveNum - 1).bCheck Then
            szWavFile = "CHECK2"
        End If
        App_mvLast = Search_mvResult
        App_frm.DrawSquare(App_mvLast And 255, DRAW_SELECTED)
        App_frm.DrawSquare(App_mvLast >> 8, DRAW_SELECTED)
        If GetResult(szWavFile) Then
            Return False
        End If
        Return True
    End Function

    Public Sub Main()
        Dim frmStartUp As StartUp, nSlash As Integer, nBackSlash As Integer

        App_szPath = System.Reflection.Assembly.GetExecutingAssembly.GetName.CodeBase
        If Left(App_szPath, 8) = "file:///" Then
            App_szPath = Mid(App_szPath, 9)
        End If
        nSlash = InStrRev(App_szPath, "/")
        nBackSlash = InStrRev(App_szPath, "\")
        If nSlash > nBackSlash Then
            App_szPath = Left(App_szPath, nSlash)
        ElseIf nSlash < nBackSlash Then
            App_szPath = Left(App_szPath, nBackSlash)
        Else
            App_szPath = ""
        End If
        Randomize()
        InitArrays()
        LoadBook()
        frmStartUp = New StartUp
        App_frm = New MainForm
        App_bExit = True
        frmStartUp.ShowDialog()
        While Not App_bExit
            App_frm.ShowDialog()
            App_bExit = True
            frmStartUp.ShowDialog()
        End While
    End Sub
End Module
